apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'application'
apply plugin: 'org.junit.platform.gradle.plugin'


compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.7
version = '1.0'


mainClassName = "org.zuinnote.hadoop.ethereum.example.driver.EthereumBlockCounterDriver"

repositories {
    mavenCentral()
    mavenLocal()
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled true
    }
}


// Integrate JaCoCo for jUnit5
project.afterEvaluate {
    def junitPlatformTestTask = project.tasks.getByName('junitPlatformTest')

    // configure jacoco to analyze the junitPlatformTest task
    jacoco {
        applyTo junitPlatformTestTask
    }

    // create junit platform jacoco task
    project.task(type: JacocoReport, "junitPlatformJacocoReport",
            {
                sourceDirectories = files("./src/main")
                classDirectories = files("$buildDir/classes/main")
                executionData junitPlatformTestTask
            })
}


configurations {
	provided
	testProvided
	testIntegrationCompile.extendsFrom testCompile
	testIntegrationRuntime.extendsFrom testRuntime
}

eclipse {

  classpath {
    plusConfigurations += [ configurations.provided ]
    plusConfigurations += [ configurations.testProvided ]
    plusConfigurations += [ configurations.testIntegrationCompile ]
	plusConfigurations += [ configurations.testIntegrationRuntime ]
  }
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
     testIntegration {
        java {
            compileClasspath += main.output + test.output + configurations.provided
            runtimeClasspath += main.output + test.output + configurations.provided
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
	}
}

dependencies {
   // hadoop lib for driver
     provided("org.apache.hadoop:hadoop-client:2.7.0")
        // bouncycastle libraries - only for Ethereum utility functions needed
    compile("org.bouncycastle:bcprov-ext-jdk15on:1.58")
       // hadoop crypto ledger library
   compile("com.github.zuinnote:hadoopcryptoledger-fileformat:1.2.0")
     // testing
    testCompile group: 'org.jmockit', name: 'jmockit', version: '1.36.3'
     testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.1.1'
     testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.1.1'
    testIntegrationRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.1.1'
	testIntegrationRuntime group: 'org.junit.platform', name: 'junit-platform-console', version: '1.1.1'
    testIntegrationCompile group: 'org.apache.hadoop', name: 'hadoop-minicluster', version: '2.7.0'


jar {
    manifest {
        attributes 'Implementation-Title': 'Example - MR job (EthereumBlock) for analysing Ethereum data using hadoopcryptoledger', 'Implementation-Version': version
    }

    baseName = 'example-hcl-mr-ethereumblock'
    version = '0.1.0'
   // note this builds one fat jar and it is not recommended for production use - just for illustration purpose
  from (configurations.compile.collect { entry -> zipTree(entry) }) {
        exclude 'META-INF/MANIFEST.MF'
        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.DSA'
        exclude 'META-INF/*.RSA'
    }
}
}

task testUnit(type: JavaExec, dependsOn: jar) {
    group 'Verification'
    description 'Runs the unit tests.'

    classpath = sourceSets.test.runtimeClasspath
    systemProperties['jar.path'] = jar.archivePath

    main 'org.junit.platform.console.ConsoleLauncher'
    args = ['--scan-classpath', sourceSets.testIntegration.output.classesDirs.asPath,
            '--reports-dir', "${buildDir}/test-results/testIntegration"
    ]
}

task testIntegration(type: JavaExec, dependsOn: jar) {
    group 'Verification'
    description 'Runs the integration tests.'
 
    classpath = sourceSets.testIntegration.runtimeClasspath
    systemProperties['jar.path'] = jar.archivePath

    main 'org.junit.platform.console.ConsoleLauncher'
    args = ['--scan-classpath', sourceSets.testIntegration.output.classesDirs.asPath,
            '--reports-dir', "${buildDir}/test-results/integration-test"
    ]
}


check.dependsOn testIntegration
testIntegration.mustRunAfter testUnit

afterEvaluate {
    jacoco {
        applyTo testUnit
        applyTo testIntegration
}
}

uploadArchives {
    repositories {
       flatDir {
           dirs 'repos'
       }
    }
}
